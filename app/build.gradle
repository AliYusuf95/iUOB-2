apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "com.muqdd.iuob2"
        minSdkVersion 21
        compileSdkVersion 26
        targetSdkVersion 26
        versionCode 7
        versionName "1.4.1"
        renderscriptTargetApi 21
        renderscriptSupportModeEnabled true
        resValue "string", "google_maps_key", (project.findProperty("GOOGLE_MAPS_API_KEY") ?: "")
    }
    buildTypes {
        release {
//            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
//            shrinkResources false
//            minifyEnabled true
//            useProguard false
            ext.enableCrashlytics = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

// dependencies versions
ext {
    versions = [
            supportLib: '27.0.2',
            firebaseLib: '11.6.2'
    ]
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // Edit photo SDK
    implementation project(':photoeditorsdk')
    // Android
    implementation "com.android.support:appcompat-v7:${versions.supportLib}"
    implementation "com.android.support:cardview-v7:${versions.supportLib}"
    implementation "com.android.support:design:${versions.supportLib}"
    // Google Play Services
    implementation "com.google.android.gms:play-services-maps:${versions.firebaseLib}"
    implementation "com.google.android.gms:play-services-location:${versions.firebaseLib}"
    implementation "com.google.android.gms:play-services-analytics:${versions.firebaseLib}"
    // Firebase
    implementation "com.google.firebase:firebase-core:${versions.firebaseLib}"
    implementation "com.google.firebase:firebase-messaging:${versions.firebaseLib}"
    implementation "com.google.firebase:firebase-crash:${versions.firebaseLib}"
    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.3.0"
    implementation "com.squareup.retrofit2:converter-gson:2.3.0"
    implementation "com.squareup.okhttp3:logging-interceptor:3.9.0"
    // MaterialDrawer
    implementation("com.mikepenz:materialdrawer:5.9.5@aar") {
        transitive = true
    }
    // Butter Knife
    implementation "com.jakewharton:butterknife:8.8.1"
    annotationProcessor "com.jakewharton:butterknife-compiler:8.8.1"
    // Logger
    implementation "com.orhanobut:logger:2.1.1"
    // Super RecyclerView
    implementation "com.malinskiy:superrecyclerview:1.1.4"
    // FastAdapter
    implementation "com.mikepenz:fastadapter-commons:2.6.3@aar"
    implementation "com.mikepenz.thirdparty:stickyheadersrecyclerview:0.4.4-SNAPSHOT@aar"
    // RoundedImageView
    implementation "com.makeramen:roundedimageview:2.3.0"
    // Material sheet fab
    implementation "com.gordonwong:material-sheet-fab:1.2.1"
    // Timeline View
    implementation "com.github.vipulasri:timelineview:1.0.5"
    // AutoFit TextView
    implementation "me.grantland:autofittextview:0.2.1"
    // Segmented Controls
    implementation "info.hoang8f:android-segmented:1.0.6"
    // View Pager Indicator
    implementation "fr.avianey.com.viewpagerindicator:library:2.4.1@aar"
    // Material spinner
    implementation ("com.github.ganfra:material-spinner:2.0.0") {
        exclude group: "com.android.support", module: "appcompat-v7"
    }
    // Camera button
    implementation 'com.dewarder:camerabutton-rxjava2:1.0.0'
    // Glide
    implementation 'com.github.bumptech.glide:glide:3.9.0-SNAPSHOT'
    // StatusStories
    compile 'com.github.RahulJanagouda:StatusStories:1.0.1'
    // SwipeBack
    compile 'com.github.liuguangqiang.swipeback:library:1.0.2@aar'
}
apply plugin: "com.google.gms.google-services"