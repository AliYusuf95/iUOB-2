plugins {
    id "com.google.secrets_gradle_plugin" version "0.6"
}
apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.crashlytics'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildToolsVersion '30.0.2'
    defaultConfig {
        applicationId "com.muqdd.iuob2"
        minSdkVersion 21
        compileSdkVersion 28
        targetSdkVersion 30
        versionCode 18
        versionName "1.5.12"
        renderscriptTargetApi 21
        renderscriptSupportModeEnabled true
//        multiDexEnabled true
        resValue "string", "google_maps_key", (project.findProperty("GOOGLE_MAPS_API_KEY") ?: "")
        resValue "string", "google_admob_id", (project.findProperty("GOOGLE_ADMOB_APP_ID") ?: "")
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            firebaseCrashlytics {
                mappingFileUploadEnabled true
            }
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            resValue "string", "google_admob_main_unit_id", (project.findProperty("GOOGLE_ADMOB_MAIN_UNIT_ID") ?: "")
            resValue "string", "google_admob_app_oen_unit_id", (project.findProperty("GOOGLE_ADMOB_APP_OPEN_UNIT_ID") ?: "")
            resValue "string", "google_admob_add_course_unit_id", (project.findProperty("GOOGLE_ADMOB_ADD_COURSE_UNIT_ID") ?: "")
        }
        debug {
            shrinkResources false
            minifyEnabled false
            debuggable true
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "google_admob_main_unit_id", "ca-app-pub-3940256099942544/6300978111"
            resValue "string", "google_admob_app_oen_unit_id", "ca-app-pub-3940256099942544/1033173712"
            resValue "string", "google_admob_add_course_unit_id", "ca-app-pub-3940256099942544/1033173712"
        }
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // Edit photo SDK
    implementation project(':photoeditorsdk')
    // Android
//    implementation 'com.android.support:multidex:1.0.3'
    implementation "com.android.support:appcompat-v7:28.0.0"
    implementation "com.android.support:cardview-v7:28.0.0"
    implementation "com.android.support:design:28.0.0"
    implementation "com.android.support:support-v4:28.0.0"
    // Android x
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    def lifecycle_version = "2.3.1"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle_version"
    // annotationProcessor "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    // Google Play Services
    implementation "com.google.android.gms:play-services-maps:17.0.1"
    implementation "com.google.android.gms:play-services-location:18.0.0"
    implementation "com.google.android.gms:play-services-analytics:17.0.0"
    implementation ("com.google.android.gms:play-services-ads") {
        version {
            strictly '19.7.0'
        }
    }
    implementation ("com.google.ads.mediation:facebook"){
        version {
            strictly '6.3.0.1'
        }
    }
    // Firebase
    implementation platform('com.google.firebase:firebase-bom:26.1.1')
    implementation "com.google.firebase:firebase-core"
    implementation "com.google.firebase:firebase-messaging"
    implementation "com.google.firebase:firebase-crashlytics"
    implementation "com.google.firebase:firebase-analytics"
    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:3.14.9"
    // MaterialDrawer
    implementation("com.mikepenz:materialdrawer:5.9.5@aar") {
        transitive = true
    }
    // Butter Knife
    implementation 'com.jakewharton:butterknife:10.2.3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'
    // Logger
    implementation "com.orhanobut:logger:2.1.1"
    // Super RecyclerView
    implementation "com.malinskiy:superrecyclerview:1.1.4"
    // FastAdapter
    implementation "com.mikepenz:fastadapter-commons:2.6.3@aar"
    implementation "com.mikepenz.thirdparty:stickyheadersrecyclerview:0.4.4-SNAPSHOT@aar"
    // RoundedImageView
    implementation "com.makeramen:roundedimageview:2.3.0"
    // Material sheet fab
    implementation "com.gordonwong:material-sheet-fab:1.2.1"
    // Timeline View
    implementation "com.github.vipulasri:timelineview:1.0.5"
    // AutoFit TextView
    implementation "me.grantland:autofittextview:0.2.1"
    // Segmented Controls
    implementation "info.hoang8f:android-segmented:1.0.6"
    // View Pager Indicator
    implementation "fr.avianey.com.viewpagerindicator:library:2.4.1@aar"
    // Material spinner
    implementation ("com.github.ganfra:material-spinner:2.0.0") {
        exclude group: "com.android.support", module: "appcompat-v7"
    }
    // Camera button
    implementation 'com.dewarder:camerabutton-rxjava2:1.0.0'
    // Glide
    implementation 'com.github.bumptech.glide:glide:3.9.0-SNAPSHOT'
    // StatusStories
    implementation 'com.github.RahulJanagouda:StatusStories:1.0.1'
}
apply plugin: "com.google.gms.google-services"